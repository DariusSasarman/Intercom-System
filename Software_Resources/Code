  #include <SPI.h>
  #include <MFRC522.h>
  #include <LiquidCrystal.h>
  #include <Stepper.h>

  /* ================== CONSTANTE ================== */

  const int stepsPerRevolution = 2048;
  const int stepsForDoor = 420;

  /* ================== PINI ================== */

  // RFID
  #define SS_PIN   10
  #define RST_PIN  8

  // LCD
  LiquidCrystal lcd(2, 3, 4, 5, 6, 7);

  // Butoane / LED / Buzzer
  #define BTN_CALL 9
  #define BUZZER   A1

  // Motor
  #define IN1 A5
  #define IN2 A4
  #define IN3 A3
  #define IN4 A2

  /* ================== VARIABILE ================== */

  bool lastButtonState = HIGH;

  /* ================== OBIECTE ================== */
  MFRC522 rfid(SS_PIN, MFRC522::UNUSED_PIN);  
  Stepper myStepper(stepsPerRevolution, IN1, IN3, IN2, IN4);

  /* ================== UID ================== */

  byte authorizedUID[4] = {0x73, 0x77, 0xF1, 0x2C};

  /* ================== FUNCÈšII ================== */

  bool checkUID(byte *uid) {
    for (byte i = 0; i < 4; i++) {
      if (uid[i] != authorizedUID[i]) return false;
    }
    return true;
  }

  void beep(int duration = 200) {
    tone(BUZZER, 3000);
  }
  void accessDenied() {
    lcd.clear();
    lcd.print("Access Denied");
    beep(500);
    delay(2000);
    noTone(BUZZER);
    lcd.clear();
    lcd.print("Ready...");
  }

  void unlockDoor() {
    lcd.clear();
    lcd.print("Access Granted");
    beep(300);

    myStepper.step(-stepsForDoor);
    delay(500);

    delay(5000);

    myStepper.step(stepsForDoor);
    delay(500);
    digitalWrite(IN1, LOW);
    digitalWrite(IN2, LOW);
    digitalWrite(IN3, LOW);
    digitalWrite(IN4, LOW);

    noTone(BUZZER);
    lcd.clear();
    lcd.print("Ready...");
  }

  /* ================== SETUP ================== */

  void setup() {

    myStepper.setSpeed(8);

    pinMode(BTN_CALL, INPUT_PULLUP);
    pinMode(BUZZER, OUTPUT);
    
    pinMode(A0, INPUT);

    pinMode(IN1, OUTPUT);
    pinMode(IN2, OUTPUT);
    pinMode(IN3, OUTPUT);
    pinMode(IN4, OUTPUT);

    lcd.begin(16, 2);
    lcd.print("Initializing...");
    
    pinMode(10, OUTPUT);   // keep SPI master mode stable
    digitalWrite(10, HIGH); 
    SPI.begin();
    rfid.PCD_Init();

    delay(1000);
    lcd.clear();
    lcd.print("Ready...");
  }

  /* ================== LOOP ================== */
  void loop() {
    bool currentButtonState = digitalRead(BTN_CALL);

    if (lastButtonState == HIGH && currentButtonState == LOW) {
      lcd.clear();
      lcd.print("Inside unlock");
      delay(3000);
      unlockDoor();
    }

    lastButtonState = currentButtonState;

    if (rfid.PICC_IsNewCardPresent() && rfid.PICC_ReadCardSerial()) {
      lcd.clear();
      lcd.print("Reading card rn");
      delay(3000);
      if (checkUID(rfid.uid.uidByte)) unlockDoor();
      else accessDenied();

      rfid.PICC_HaltA();
      rfid.PCD_StopCrypto1();
      lcd.clear();
      lcd.print("ready");
    }
  }
